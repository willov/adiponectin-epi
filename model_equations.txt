********** MODEL NAME
adr_endo
********** MODEL NOTES

********** MODEL STATES
d/dt(B) = vBde_B - vB_Bact
d/dt(Bact) = vB_Bact - vBact_Bde
d/dt(Bde)= vBact_Bde - vBde_B

d/dt(Ca) = vPip_Ca - vremCa
d/dt(ATP) = vPip_ATP - vdegATP
d/dt(cAMP) = vPip_cAMP + vBactcAMP - vdegcAMP

d/dt(Res) = 0
d/dt(Rel)= vRes_Rel - vRel_PM + vPM_Rel
d/dt(PM)= vRel_PM - vPM_Rel - vPM
d/dt(Endo)=vCa_Endo-vEndo
d/dt(Adiponectin)=vPM

d/dt(pipCa) = - vPip_Ca*Vcell/Vpip
d/dt(pipATP) = - vPip_ATP*Vcell/Vpip
d/dt(pipcAMP) = - vPip_cAMP*Vcell/Vpip

B(0)=100
Bact(0)=0
Bde(0)=0

Ca(0)= 0
ATP(0)= 0
cAMP(0)= 0

Res(0)= 99
Rel(0)= 1
PM(0)= 0
Endo(0)=0
Adiponectin(0)=0

pipCa(0)=0
pipATP(0)=0
pipcAMP(0)=0

********** MODEL PARAMETERS
krel= 1
kexo= 1
kCaATP=1
kcAMP= 1
kCa2=1
kATP2=1
kEndo=1
kCacAMP=1
km=1

kdegcAMP=1
kremCa=1
kdegATP=1

kDiffcAMP=1
kDiffCa=1
kDiffATP=1

kB=1
kCL=1
k1=1
k2=1
k3=1

Vpip=1
Vcell=1

EPI=1
CL=5
pip=1

********** MODEL VARIABLES
% Measurement variables
u=PM*kexo-kEndo*Endo
uEndo = kEndo*Endo
uExo  = PM*kexo

********** MODEL REACTIONS
vPip_cAMP = kDiffcAMP*(pipcAMP - cAMP)*pip
vPip_Ca = kDiffCa*(pipCa - Ca)*pip
vPip_ATP = kDiffATP*(pipATP - ATP)*pip

vB_Bact  = kB*B*(EPI + kCL*CL )
vBact_Bde = Bact*k2
vBde_B = k1*Bde

vBactcAMP = Bact*k3
vdegcAMP = kdegcAMP*cAMP 
vremCa = kremCa*Ca 
vdegATP = kdegATP*ATP 

vRes_Rel= ((Ca/(km+Ca))*(kCa2+kATP2*ATP))*Res
vRel_PM= cAMP*(kcAMP + (Ca/(km+Ca))*ATP*kCaATP)*Rel
vPM_Rel= krel*PM
vPM= kexo*PM

vCa_Endo=kCacAMP*cAMP*Ca
vEndo = kEndo*Endo


********** MODEL FUNCTIONS


********** MODEL EVENTS

********** MODEL MATLAB FUNCTIONS